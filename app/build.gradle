apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.zhouchao.test"
        minSdkVersion 27
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //编译libs下的aar包
    repositories {
        flatDir {
            dirs 'libs'
            //dirs project(':moduleName').file('libs') //如果有module且依赖了aar库，需要此种写法，不然会报failed to resolve错误
        }
    }

    //指定jni路径，识别libs下的.so文件（默认只识别src/main/jniLibs下的文件。还有一种方法：把.so文件打包成jar放到libs下）
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    //打包apk重命名
    applicationVariants.all { variant ->
        variant.outputs.all {
            def time = new Date().format("yyyy-MM-dd", TimeZone.getDefault())
            outputFileName = "Test-${time}-v${defaultConfig.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //①编译libs-sdk下的*.jar；②另外最外层的build.gradle中需加 allprojects {xxx}
    compileOnly fileTree(include: ['*.jar'], dir: 'libs-sdk')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.robinhood.ticker:ticker:2.0.2'
}
